Value ifname (\S+)
Value state (\w+)
Value ipAddress ([0-9./]+)
Value vrf (\S+)
Value area ([0-9.]+)
Value networkType (\w+)
Value cost (\d+)
Value helloTime (\d+)
Value deadTime (\d+)
Value retxTime (\d+)
Value nbrCount (\d+)
Value passive (\w+)

Start
  ^\w+ is.*$$ -> Continue.Record
  ^${ifname} is ${state}.*$$
  ^\s+Interface Address ${ipAddress}, VRF ${vrf}, Area ${area}.*$$
  ^\s+Network Type ${networkType}, Cost:\s+${cost}.*$$
  ^\s+Timer intervals configured, Hello ${helloTime}, Dead ${deadTime}, Retransmit ${retxTime}.*$$
  ^\s+Neighbor Count is ${nbrCount} \(${passive} Interface\)
  ^\s+Neighbor Count is ${nbrCount}

#Ethernet1 is up
#  Interface Address 10.127.0.1/31, VRF default, Area 0.0.0.0
#  Network Type Broadcast, Cost: 10
#  Transmit Delay is 1 sec, State DR, Priority 1
#  Designated Router is 10.254.0.1
#  Backup Designated Router is 10.254.0.2
#  Timer intervals configured, Hello 10, Dead 40, Retransmit 5
#  Neighbor Count is 1
#  No authentication
#Ethernet2 is up
#  Interface Address 10.127.0.3/31, VRF default, Area 0.0.0.0
#  Network Type Broadcast, Cost: 10
#  Transmit Delay is 1 sec, State DR, Priority 1
#  Designated Router is 10.254.0.1
#  Backup Designated Router is 10.254.0.2
#  Timer intervals configured, Hello 10, Dead 40, Retransmit 5
#  Neighbor Count is 1
#  No authentication
#Vlan10 is up
#  Interface Address 10.10.0.1/24, VRF default, Area 0.0.0.0
#  Network Type Broadcast, Cost: 10
#  Transmit Delay is 1 sec, State DR, Priority 1
#  Designated Router is 10.254.0.1
#  No Backup Designated Router on this network
#  Timer intervals configured, Hello 10, Dead 40, Retransmit 5
#  Neighbor Count is 0 (Passive Interface)
#  No authentication
#Loopback0 is up
#  Interface Address 10.254.0.1/32, VRF default, Area 0.0.0.0
#  Network Type Broadcast, Cost: 10
#  Transmit Delay is 1 sec, State DR, Priority 1
#  Designated Router is 10.254.0.1
#  No Backup Designated Router on this network
#  Timer intervals configured, Hello 10, Dead 40, Retransmit 5
#  Neighbor Count is 0
#  No authentication
#